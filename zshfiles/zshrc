# _____________________________/\\\___________________________________
#  ____________________________\/\\\___________________________________
#   ____________________________\/\\\___________________________________
#    __/\\\\\\\\\\\__/\\\\\\\\\\_\/\\\__________/\\/\\\\\\\____/\\\\\\\\_
#     _\///////\\\/__\/\\\//////__\/\\\\\\\\\\__\/\\\/////\\\_/\\\//////__
#      ______/\\\/____\/\\\\\\\\\\_\/\\\/////\\\_\/\\\___\///_/\\\_________
#       ____/\\\/______\////////\\\_\/\\\___\/\\\_\/\\\_______\//\\\________
#        __/\\\\\\\\\\\__/\\\\\\\\\\_\/\\\___\/\\\_\/\\\________\///\\\\\\\\_
#         _\///////////__\//////////__\///____\///__\///____________\////////_
# =============================================================================
# export variables                                                          {{{
# =============================================================================

# set the zsh root directoy
export ZSH_ROOT=$HOME/.zsh

export ZSH_CACHE_DIR=$ZSH/cache

# PATH to /usr/local/lib
export LD_LIBRARY_PATH=/usr/local/lib:$LD_LIBRARY_PATH

# set language environment
export LANG=en_US.UTF-8
export LC_TIME=en_US.UTF-8

# set PATH
export PATH="$HOME/bin:/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin:/usr/games:/usr/local/games"
export MANPATH="/usr/local/man:$MANPATH"

# find alternative apps if it is installed on your system
find_alt() { for i;do hash "$i" >/dev/null && { echo "$i"; return 0;};done;return 1; }

# set the default program
# the first program in the array will be chosen as the default
export OPENER=$(find_alt xdg-open exo-open gnome-open )
export BROWSER=$(find_alt luakit firefox google-chrome chromium chromium-browser $OPENER )
export BROWSERCLI=$(find_alt w3m links2 links lynx elinks $OPENER )
export EDITOR=$(find_alt vim emacs nano leafpad gedit pluma $OPENER )
export FILEMANAGER=$(find_alt thunar nautilus dolphin pcmanfm tspacefm enlightenment_filemanager $OPENER )
export PAGER=$(find_alt less more most)
export PLAYER=$(find_alt mpv mplayer cvlc $OPENER )
export READER=$(find_alt mupdf zathura evince $OPENER )
export TERMCMD=$(find_alt urxvt xterm gnome-terminal)

# used to store all the repo clones for antigen
export ADOTDIR=$ZSH/antigen

# enhancd by "c"
export ENHANCD_COMMAND='c'

# takenote
export TAKENOTE_ROOTDIR="$HOME/Workspace/blog"
export TAKENOTE_FILERCMD=ranger

# ==========================================================================}}}
# zgen                                                                      {{{
# =============================================================================

ZGEN_AUTOLOAD_COMPINIT=false

zgen_repo_url="https://github.com/tarjoilija/zgen.git"
zgen_raw_url="https://raw.githubusercontent.com/tarjoilija/zgen.git"

# where to clone antigen
zgen_root="${HOME}/.zsh/plugins/zgen"

# if Antigen is not installed, automatically clone it and reload shell
if [[ ! -f "${zgen_root}/zgen.zsh" ]]; then
  echo "zgen is not installed in this machine."
  echo "Installing zgen..."
  echo ""
  if hash git; then
    install -d "${zgen_root}"
    git clone "${zgen_repo_url}" "${zgen_root}"
  elif hash curl; then
    install -d "${zgen_root}"
    curl -L "${zgen_raw_url}" -o "${zgen_root}/zgen.zsh"
  elif hash wget; then 
    install -d "${zgen_root}"
    wget "${zgen_raw_url}" -O "${zgen_root}/zgen.zsh"
  else
    echo "git or curl or wget required to install."
    echo "Aborted."
    exit 1
  fi
  exec zsh -l
fi

source "${zgen_root}/zgen.zsh"

# check if there's no init script
if ! zgen saved; then
    echo "Creating a zgen save"

    zgen load zsh-users/zsh-syntax-highlighting
    zgen load robbyrussell/oh-my-zsh lib/git.zsh
    zgen load robbyrussell/oh-my-zsh lib/grep.zsh
    zgen load robbyrussell/oh-my-zsh lib/history.zsh
    zgen load robbyrussell/oh-my-zsh lib/spectrum.zsh
    zgen load robbyrussell/oh-my-zsh lib/termsupport.zsh
    zgen load robbyrussell/oh-my-zsh lib/theme-and-appearance.zsh
    zgen load fcambus/ansiweather
    zgen load b4b4r07/enhancd zsh
    zgen load Tarrasch/zsh-bd
    zgen load chrissicool/zsh-256color
    zgen load ssh0/zsh-takenote

    # save all to init script
    zgen save
fi

# ==========================================================================}}}
# history                                                                   {{{
# =============================================================================

HISTFILE=${ZSH_ROOT}/history

# ignore duplication command history list
setopt hist_ignore_all_dups

# ==========================================================================}}}
# completions                                                               {{{
# =============================================================================

# add fpath for completions
# there are completion functoins named like '_function'.
fpath=($ZSH_ROOT/completions $fpath)

unsetopt flowcontrol
setopt auto_menu         # show completion menu on succesive tab press
setopt interactive_comments
setopt complete_in_word
setopt always_to_end
setopt auto_param_slash

export WORDCHARS=''

zstyle ':completion:*' verbose yes
zstyle ':completion:*' format '%B%F{magenta}%d%b%f'

zstyle ':completion:*:default' menu select=2
zstyle ':completion:*:messages' format '%F{yellow}%d'"$DEFAULT"
zstyle ':completion:*:warnings' format '%F{red}No matches for:''%F{yelllow} %d'"$DEFAULT"
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:descriptions' format '%F{yellow}Completing %B%d%b%f'"$DEFAULT"

zstyle ':completion:*' group-name ''

# case sensitive (but if upper case, don't translate it to lower case.)
zstyle ':completion:*' matcher-list 'm:{a-zA-Z-_}={A-Za-z_-}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"

zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"

# disable named-directories autocompletion
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories

# Use caching so that commands like apt and dpkg complete are useable
zstyle ':completion::complete:*' use-cache true
zstyle ':completion::complete:*' cache-path "$ZSH_CACHE_DIR"

# Don't complete uninteresting users
zstyle ':completion:*:*:*:users' ignored-patterns \
        adm amanda apache at avahi avahi-autoipd beaglidx bin cacti canna \
        clamav daemon dbus distcache dnsmasq dovecot fax ftp games gdm \
        gkrellmd gopher hacluster haldaemon halt hsqldb ident junkbust kdm \
        ldap lp mail mailman mailnull man messagebus  mldonkey mysql nagios \
        named netdump news nfsnobody nobody nscd ntp nut nx obsrun openvpn \
        operator pcap polkitd postfix postgres privoxy pulse pvm quagga radvd \
        rpc rpcuser rpm rtkit scard shutdown squid sshd statd svn sync tftp \
        usbmux uucp vcsa wwwrun xfs '_*'

# ... unless we really want to.
zstyle '*' single-ignored show

# history search
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end

autoload -U compinit
compinit

# ==========================================================================}}}
# source some files                                                         {{{
# =============================================================================

# set the prompt theme
source "${ZSH_ROOT}/themes/agnoster.zsh-theme"

eval $(dircolors "${ZSH_ROOT}/dircolors/dircolors-zenburn")

# tmux plugin
source "${ZSH_ROOT}/plugins/tmux/tmux.plugin.zsh"

# command-not-found
if [[ -x /usr/lib/command-not-found ]] ; then
  function command_not_found_handler() {
  /usr/lib/command-not-found --no-failure-msg -- "$1"
}
fi

# some useful aliases
source "${ZSH_ROOT}/aliases.zsh"

# user specific configuration
if [[ -f "${ZSH_ROOT}/rc.mine" ]]; then
  source "${ZSH_ROOT}/rc.mine"
fi

# ==========================================================================}}}
# bindkeys                                                                  {{{
# =============================================================================

bindkey -e

# edit command line in vim:
# by pressing: ^xe
# Edit the current command line in $EDITOR
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^xe' edit-command-line

# Keybinding
bindkey '^r' peco-select-history

# reverse-menu-complete by `Shift+Tab`
bindkey '^[[Z' reverse-menu-complete

# by C-p, C-n
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end

# ==========================================================================}}}
