#compdef dot

_dot() {
  # local context curcontext="$curcontext" state line
  local configfile="$HOME/.dotfiles/setup_config_link"
  local dotrepo="$HOME/.dotfiles/"
  _arguments -C \
    '1: :->cmds' \
    '-h[Show this help message.]' \
    '*::arg:->args' \
    && ret=0

  case $state in
    cmds)
      _values "dot commands"\
        "clone[Clone ssh0's dotfile repository on your computer.]" \
        "pull[Pull remote dotfile repository (by git).]" \
        "set[Make symbolic link interactively.]" \
        "add[Move the file to the dotfile dir, make the link, and edit '$configfile'.]" \
        "unlink[Unlink the selected symbolic link and copy its original file from the dotfile repository.]" \
        "clear[Remove the all symbolic link in the config file '$configfile'.]" \
      && ret=0
      ;;
    args)
      local subcmd=$line[1]
      case $subcmd in
        "clone"|"unlink")
          _path_files && ret=0
          ;;
        "add")
          _arguments \
            '1: :_path_files' \
            '2:dotfiles repository:_files -W $dotrepo -P $dotrepo' \
            && ret=0
          ;;
        "set")
          _values "options"\
            '-i[No interaction mode(skip all conflicts).]' \
            '-v[Print verbose messages.]' \
            && ret=0
          ;;
      esac
      ;;
  esac

  return ret
}
